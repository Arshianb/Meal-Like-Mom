apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.rigger.publishCode
        versionName rootProject.ext.rigger.publishVersion

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility rootProject.ext.android.targetCompatibilityVersion
        targetCompatibility rootProject.ext.android.targetCompatibilityVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation rootProject.ext.dependencies.appcompatV7
    implementation rootProject.ext.dependencies.aspectJ
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publish {
    userOrg = rootProject.ext.rigger.userOrg
    groupId = rootProject.ext.rigger.groupId
    artifactId = rootProject.ext.rigger.artifactId
    publishVersion = rootProject.ext.rigger.publishVersion
    desc = rootProject.ext.rigger.desc
    website = rootProject.ext.rigger.website
}